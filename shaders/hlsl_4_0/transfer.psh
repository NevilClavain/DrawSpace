/* -*-LIC_BEGIN-*- */
/*
*                                                                          
* DrawSpace Rendering engine                                               
* Emmanuel Chaumont Copyright (c) 2013-2020                     
*                                                                          
* This file is part of DrawSpace.                                          
*                                                                          
*    DrawSpace is free software: you can redistribute it and/or modify     
*    it under the terms of the GNU General Public License as published by  
*    the Free Software Foundation, either version 3 of the License, or     
*    (at your option) any later version.                                   
*                                                                          
*    DrawSpace is distributed in the hope that it will be useful,          
*    but WITHOUT ANY WARRANTY; without even the implied warranty of        
*    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         
*    GNU General Public License for more details.                          
*                                                                          
*    You should have received a copy of the GNU General Public License     
*    along with DrawSpace.  If not, see <http://www.gnu.org/licenses/>.    
*
*/
/* -*-LIC_END-*- */

cbuffer legacyargs : register(b0)
{
    float4 vec[512];
    Matrix mat[512];
};

Texture2D txDiffuse         : register(t0);
SamplerState sam            : register(s0);

Texture2D txZMask           : register(t1);
SamplerState samZMask       : register(s1);


struct PS_INTPUT 
{
    float4 Position : SV_POSITION;
	float2 TexCoord0: TEXCOORD0;
};

#include "mat_input_constants.hlsl"
#include "fbm.hlsl"

float2 rayBoxDst(float3 boundsMin, float3 boundsMax, float3 rayOrigin, float3 rayDir)
{
	float3 t0;
	float3 t1;
	float3 tmin;
	float3 tmax;

	if (abs(rayDir.x) > 0.0)
	{
		t0.x = (boundsMin.x - rayOrigin.x) / rayDir.x;
		t1.x = (boundsMax.x - rayOrigin.x) / rayDir.x;
		
		tmin.x = min(t0.x, t1.x);
		tmax.x = max(t0.x, t1.x);
	}
	else
	{
		tmin.x = -2000000000;
		tmax.x = 2000000000;
	}

	if (abs(rayDir.y) > 0.0)
	{
		t0.y = (boundsMin.y - rayOrigin.y) / rayDir.y;
		t1.y = (boundsMax.y - rayOrigin.y) / rayDir.y;

		tmin.y = min(t0.y, t1.y);
		tmax.y = max(t0.y, t1.y);
	}
	else
	{
		tmin.y = -2000000000;
		tmax.y = 2000000000;
	}

	if (abs(rayDir.z) > 0.0)
	{
		t0.z = (boundsMin.z - rayOrigin.z) / rayDir.z;
		t1.z = (boundsMax.z - rayOrigin.z) / rayDir.z;

		tmin.z = min(t0.z, t1.z);
		tmax.z = max(t0.z, t1.z);
	}
	else
	{
		tmin.z = -2000000000;
		tmax.z = 2000000000;
	}

	float dstA = max(max(tmin.x, tmin.y), tmin.z);
	float dstB = min(tmax.x, min(tmax.y, tmax.z));

	float dstToBox = max(0, dstA);
	float dstInsideBox = max(0, dstB - dstToBox);

	return float2(dstToBox, dstInsideBox);
}


#define STEP_SIZE		0.5f


bool inVolume(float3 boundsMin, float3 boundsMax, float3 position)
{
	bool status = false;
	if (boundsMin.x < position.x && position.x < boundsMax.x)
	{
		if (boundsMin.y < position.y && position.y < boundsMax.y)
		{
			if (boundsMin.z < position.z && position.z < boundsMax.z)
			{
				status = true;
			}
		}
	}
	return status;
}

float computeLocaleDensity(float3 position)
{	
	return 1.0;
}

#define AMPLITUDE_FACTOR 0.45f //0.707f
#define FREQUENCY_FACTOR 2.1f //2.5789f

float SampleMediumDensity(float3 position, float seed1, float seed2)
{
	float3 UVW = position * 0.1;  // Let’s start with some low frequency
	float Amplitude = 1.0;
	float DensityFactor = 1.0;
	float DensityBias = 0.0;

	float V = Amplitude * Perlin3D(UVW, seed1, seed2);
	Amplitude *= AMPLITUDE_FACTOR; 
	UVW *= FREQUENCY_FACTOR;

	V += Amplitude * Perlin3D(UVW, seed1, seed2);
	Amplitude *= AMPLITUDE_FACTOR;
	UVW *= FREQUENCY_FACTOR;

	V += Amplitude * Perlin3D(UVW, seed1, seed2);
	Amplitude *= AMPLITUDE_FACTOR;
	UVW *= FREQUENCY_FACTOR;

	V += Amplitude * Perlin3D(UVW, seed1, seed2);
	Amplitude *= AMPLITUDE_FACTOR;
	UVW *= FREQUENCY_FACTOR;

	return clamp(DensityFactor * V + DensityBias, 0, 1);
}

float Ei(float z) 
{
	return 0.5772156649015328606065 + log(1e-4 + abs(z)) + z * (1.0 + z * (0.25 + z * ((1.0 / 18.0) + z * ((1.0 / 96.0) + z * (1.0 / 600.0))))); 
}

float3 computeAmbientColor(float3 position, float extinctionCoeff)
{
	float3 container_half_dims = vec[9].xyz;

	float hp = container_half_dims.y - position.y;
	float hb = position.y - (-container_half_dims.y);

	float a = -extinctionCoeff * hb;
	float3 isotropicScatteringTop = 0.99 * max(0.0, exp(a) - a * Ei(a));
	
	a = -extinctionCoeff * hp;
	float3 isotropicScatteringBottom = 0.05 * max(0.0, exp(a) - a * Ei(a));

	float3 sum = isotropicScatteringTop + isotropicScatteringBottom;

	return clamp(1.0 - sum * 1.5, 0.0, 1.0);
}


float rayleigh(float3 light_dir, float3 ray_dir)
{
	float3 n1 = normalize(-ray_dir);
	float3 n2 = normalize(-light_dir);

	float costheta = dot(n1, n2);


	return 3 * (1 + costheta * costheta) / (16.0 * 3.1415927);
}

float4 raymarch(float3 boundsMin, float3 boundsMax, float3 origin, float3 direction, float3 color_source, bool zmask, float dstLimit, float dstToBox)
{
	float4 color_res;
	
	float current_step_size = STEP_SIZE;

	bool ignore_zmask = false;

	if (inVolume(boundsMin, boundsMax, origin))
	{
		ignore_zmask = true;
	}

	float3 scattering = 0.0;
	float extinction = 1.0; // 1.0 : object total extinction

	float scatteringFactor = 1.0;
	float extinctionFactor = 1.0;

	float dstTravelled = 0;

	while (dstTravelled < dstLimit)
	{
		float3 position = origin + direction * (dstToBox + dstTravelled);

		float density = SampleMediumDensity(position, 67.3, 4344.8);
		float scatteringCoeff = scatteringFactor * density;
		float extinctionCoeff = extinctionFactor * density;

		extinction *= exp(-extinctionCoeff * current_step_size);

		float3 ab = computeAmbientColor(position, extinctionCoeff);

		float3  stepScattering = scatteringCoeff * current_step_size * ab;

		scattering += extinction * stepScattering;

		dstTravelled += current_step_size;
	}

	if (ignore_zmask)
	{
		color_res.rgb = scattering;
		color_res.a = extinction;
	}
	else
	{
		if (zmask)
		{
			color_res.rgb = scattering;
			color_res.a = extinction;
		}
		else
		{
			color_res.rgb = color_source;
			color_res.a = 1.0;
		}
	}
	return color_res;
}

float4 ps_main(PS_INTPUT input) : SV_Target
{
	float4 color_dest;

	float3 light_dir = { 1.0, -0.4, 0.0 };

	float4 camera_params = vec[0];
	Matrix mat_cam = mat[1];
	Matrix mat_pos = mat[5];
	float3 container_half_dims = vec[9].xyz;


	float camera_width = camera_params.x;
	float camera_height = camera_params.y;
	float camera_zn = camera_params.z;

	float3 ray_origin;
	
	Matrix pos_rot = mat_pos;
	pos_rot[3][0] = 0.0;
	pos_rot[3][1] = 0.0;
	pos_rot[3][2] = 0.0;
	pos_rot = transpose(pos_rot); // inverse rot mat


	ray_origin.x = mat_cam[3][0];
	ray_origin.y = mat_cam[3][1];
	ray_origin.z = mat_cam[3][2];


	float3 container_pos;
	container_pos.x = mat_pos[3][0];
	container_pos.y = mat_pos[3][1];
	container_pos.z = mat_pos[3][2];

	float3 globalcontainer_rayOrigin = ray_origin - container_pos;
	float3 localcontainer_rayOrigin = mul(globalcontainer_rayOrigin, pos_rot);



	// compute camera-local ray direction
	float4 localcam_rayDir;
	localcam_rayDir.x = (input.TexCoord0.x - 0.5) * camera_width;
	localcam_rayDir.y = ((1.0 - input.TexCoord0.y) - 0.5) * camera_height;
	localcam_rayDir.z = -camera_zn;
	localcam_rayDir.w = 1.0;

	// compute global ray direction
	mat_cam[3][0] = 0.0;
	mat_cam[3][1] = 0.0;
	mat_cam[3][2] = 0.0;
	float4 global_rayDir = mul(localcam_rayDir, mat_cam);

	// compute local container ray direction
	float4 local_rayDir = mul(global_rayDir, pos_rot);

	// compute container-local light direction
	float3 local_light_dir = mul(light_dir, pos_rot);
	
	float3 bounds_min = - container_half_dims;
	float3 bounds_max = container_half_dims;

	float2 intersection = rayBoxDst(bounds_min, bounds_max, localcontainer_rayOrigin, local_rayDir.xyz);

	float dstToBox = intersection.x;
	float dstInsideBox = intersection.y;
	
	float4 color_source = txDiffuse.Sample(sam, input.TexCoord0);
	float4 zmask        = txZMask.Sample(samZMask, input.TexCoord0);

	
	float4 raymarch_res = raymarch(bounds_min, bounds_max, localcontainer_rayOrigin, local_rayDir.xyz, color_source.rgb, zmask.x, dstInsideBox, dstToBox);
	float3 scattering = raymarch_res.rgb;
	float extinction = raymarch_res.a;

	// extinction = 1.0 -> object total transparency -> show color_source

	color_dest.rgb = lerp(scattering, color_source.rgb, extinction);
	color_dest.a = 1.0;
	
	// flat volume, en cas de besoin...
	/*
	if (dstInsideBox > 0)
	{
		color_dest = 1.0;
	}
	else
	{
		color_dest = color_source;
	}
	*/
	
	return color_dest;
}
